plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'pl.allegro.tech.build.axion-release' version '1.12.1'
    id 'com.palantir.docker' version '0.25.0'
}

group 'com.matteoveroni.resttest'
mainClassName = "com.matteoveroni.resttest.App"
scmVersion.repository.customUsername = project.property("GITHUB_TOKEN")
project.version = scmVersion.version

repositories {
    jcenter()
}

dependencies {
    implementation group: 'io.javalin', name: 'javalin', version: '3.12.0'
    implementation "org.slf4j:slf4j-simple:1.8.0-beta4"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest.attributes(
            'Implementation-Title': 'RestTest App',
            'Specification-Version': project.version.replace('-SNAPSHOT', ''),
            'Implementation-Version': project.version,
            'Main-Class': mainClassName
    )
}

// Plugin per creazione fatjar
shadowJar {
    archiveClassifier.set('')
    archiveVersion.set(project.version)
}

docker {
    name "${project.name}:${project.version}"
    files "$buildDir/libs/${project.name}-${project.version}.jar"
    buildArgs([JAR_FILE: "${project.name}-${project.version}.jar"])
}